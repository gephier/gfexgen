buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.1.2'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileJava.options.encoding = 'UTF-8'

jar {
	baseName 'gefx_gen'
    manifest {
        attributes 'Main-Class': 'kr.gfex.main.GfexMain'
    }
}

task distZip(type: Zip){
	def baseDir = { archiveName - ".zip" }
	into("/") {
		// shell 파일은 실행권한을 주기 위해 별도로 포함시킨다.
		from(project.file("src/dist")) {
	        exclude('**/*.sh')
	    }
		from(project.file("src/dist")) {
	        include('**/*.sh')
	        fileMode = 0755
	    }
	}
	into("lib") {
	    from(jar)
	    from(project.configurations.runtime)
	}
}

task unzip(type: Copy) {
	from(zipTree('build/distributions/GEFX_GEN.zip'))
	into '/dist/'
}

unzip.dependsOn distZip

repositories {
	flatDir {
		dirs 'lib'
	}
    mavenCentral()
}

apply plugin: 'de.undercouch.download'

import de.undercouch.gradle.tasks.download.Download

task downloadGephi(type: Download) {
    src 'http://github.com/gephi/gephi-toolkit/releases/download/v0.9.1/gephi-toolkit-0.9.1-all.jar'
    dest 'lib/'
    overwrite false
}

compileJava.dependsOn downloadGephi

dependencies {

	// gephi toolkit
	compile name: 'gephi-toolkit-0.9.1-all'

	// logback
	compile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.7'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

	// junit
    testCompile group: 'junit', name: 'junit', version: '4.+'
}
